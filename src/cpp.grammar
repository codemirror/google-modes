extends "./c.grammar"

skip whitespace {
  context Statement {
    Class |
    namespace declName? (op("=") variableName ";" | Block) |
    throw Expression ";" |
    try Block (catch "(" Type Declarator? ")" Block)* |
    asm "{" string "}" |
    using (typename Type | namespace? variableName) ("=" Type)? ";" |
    Template |
    &constructorAhead declName FunctionDef |
    nameQualifier+ (MaybeType DeclRest | qualifiedVariableName ExpressionSuffix* ";") |
    super
  }

  TypeSuffix {
    TemplateArgs |
    "::" Type |
    super
  }

  BaseExpression {
    this |
    Lambda |
    cast "<" Type ">" "(" Expression ")" |
    super
  }

  ExpressionNoGt {
    ExpressionPrefix* BaseExpression (!">" ExpressionSuffix)*
  }

  ExpressionSuffix {
    BlockOf(Expression, ",") |
    ArgList Block? |
    super
  }

  Lambda {
    "[" ListOf("&" variableName? | "=" | this) "]" FunctionDef
  }

  context FunctionDef {
    ParamList
    qualifier*
    ("&" | "&&")?
    (noexcept ("(" Expression ")")? | throw "(" ListOf(Type) ")")?
    ("->" Type)?
    InitializerList?
    (";" | Block)
  }

  context Template {
    export? template TemplateParams whitespace Statement
  }

  context InitializerList { (":" ListOf(CtorInitializer)) }

  CtorInitializer { fieldName ("(" ListOf(Expression) ")" | "{" ListOf(Expression) "}") }

  ExpressionPrefix {
    delete ("[" "]")? | new ("[" "]")? ("(" Expression ")")? | super
  }

  Type {
    typename Type | super
  }

  context TemplateParams {
    "<" ListOf(TypeParam | modifier* Type declName (op("=") ExpressionNoGt)?) ">"
  }

  TypeParam {
     (template TemplateArgs)? (class | typename) typeDeclName (op("=") (variableName | number))?
  }

  context TemplateArgs {
    "<" ListOf(TypeArg | modifier* Type Initializer? | number) ">"
  }

  TypeArg {
     (template TemplateArgs)? (class | typename) Type
  }

  Class {
    (class | structUnion) (alignas "(" number ")")? declName (":" virtual? access? Type)? (";" | ClassBody)
  }

  context ClassBody {
    "{" (access ":" | Template | ObjectMember)* "}"
  }

  context ObjectMember {
    (modifier | qualifier)*
    (ctorDtorName | Type Declarator)
    (FunctionDef | (Initializer | ":" number | "[" number? "]")? ";")
  }

  Initializer { super | BlockOf(Expression, ",") | ArgList }
}

tokens {
  namespace { kw("namespace") }
  typename { kw("typename") }
  new { kw("new") }
  delete { kw("delete") }
  this { kw("this") }
  try { kw("try") }
  catch { kw("catch") }
  throw { kw("throw") }
  noexcept { kw("noexcept") }
  asm { kw("asm") }
  export { kw("export") }
  template { kw("template") }
  class { kw("class") }
  virtual { kw("virtual") }
  access { kw("private" | "public" | "protected") }
  operator { kw("operator") }
  using { kw("using") }
  alignas { kw("alignas") }
  cast { kw(("dynamic" | "static" | "const" | "reinterpret") "_cast") }
  atom="atom" { "nullptr" !identifierChar | super }


  variableName {
    nameQualifier+ ((qualifiedTemplateName TemplateArgs)  | initializedName | qualifiedVariableName) |
    initializedName |
    unqualifiedTemplateName TemplateArgs |
    unqualifiedVariableName
  }

  modifier { kw("inline" | "virtual" | "explicit" | "friend" | "constexpr") | super }

  prefixOp="operator" {
    typename | super
  }

  number="number" {
    ("0x" (digit | "a"-"f" | "A"-"F" | "'")+ |
     "0b" ("0" | "1" | "'")+ |
     (digit digits ("." digits)? | "." digit digits) (("e" | "E") "-"? digits)?)
    ("L" | "l" | "U" | "u" | "F" | "f")?
  }

  string="string" {
    "R\"(" (!")\"" _)* ")\"" | super
  }
}

digits { (digit | "'")* }

nameQualifier="meta" { "::" | identifier " "* "::" }

colons="meta" { "::" }

parameterizedQualifier="meta" { identifier ~(" "* "<") }

declQualifier { parameterizedQualifier whitespace TemplateArgs colons qualifiedDeclName | nameQualifier }

declName { operator (binOp | prefixOp) | declQualifier+ qualifiedDeclName | unqualifiedDeclName }

typeDeclName="type def" { identifier }

declOp { "*" | "&&" | "&" }

unqualifiedDeclName="def" { "~"? identifier }
qualifiedDeclName="qualified def" { "~"? identifier }

initializedName="type" { identifier ~"{" }

unqualifiedVariableName="variable" { maybeCallee }

qualifiedVariableName="qualified" { unqualifiedVariableName }

qualifiedTemplateName="variable qualified" { identifier ~"<" }
unqualifiedTemplateName="variable" { identifier ~"<" }

typeNameInner { (typename whitespace)? nameQualifier* unqualifiedTypeName }

unqualifiedTypeName="type" { identifier }

maybeTypeName {
  typename whitespace nameQualifier* unqualifiedTypeName |
  !(("new" | "delete") !identifierChar) nameQualifier* unqualifiedTypeName ~(whitespace TypeSuffix* whitespace identifierStart)
}

ctorDtorName="def" { &localConstructorAhead "~"? identifier }
